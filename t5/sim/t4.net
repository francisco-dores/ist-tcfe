.options savecurrents

.param RS_value=100
.param RB1_value=79k
.param RB2_value=9.2k
.param RC1_value=2.4k
.param RE1_value=150
.param RE2_value=320
.param RL_value=8
.param Ci_value=5u
.param Cb_value=270u
.param Co_value=120u

.csparam RS_value={RS_value}
.csparam RB1_value={RB1_value}
.csparam RB2_value={RB2_value}
.csparam RC1_value={RC1_value}
.csparam RE1_value={RE1_value}
.csparam RE2_value={RE2_value}
.csparam RL_value={RL_value}
.csparam Ci_value={Ci_value}
.csparam Cb_value={Cb_value}
.csparam Co_value={Co_value}

* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)

Vcc vcc 0 12.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)
RS in in2 {RS_value}

* input coupling capacitor
Ci in2 base {Ci_value}

* bias circuit
RB1 vcc base {RB1_value}
RB2 base 0 {RB2_value}

* gain stage
Q1 coll base emit BC547A
RC1 vcc coll {RC1_value}
RE1 emit 0 {RE1_value}

* bypass capacitor
Cb emit 0 {Cb_value}


* output stage
Q2 0 coll emit2 BC557A
RE2 emit2 vcc {RE2_value}

* output coupling capacitor
Co emit2 out {Co_value}

* load
RL out 0 {RL_value}

.op
.end

.control

op

* time analysis
tran 1e-5 1e-2


*plot v(coll)
hardcopy vo1.ps vdb(coll)
echo vo1_FIG

* frequency analysis
ac dec 10 10 100MEG

*plot vdb(coll)
hardcopy vcolldb.ps vdb(coll)
echo vcolldb_FIG
*plot vp(coll)
hardcopy vcollp.ps vp(coll)
echo vcollp_FIG

*plot vdb(out)
hardcopy voutdb.ps vdb(out)
echo voutdb_FIG
*plot vp(out)
hardcopy voutp.ps vp(out)
echo voutp_FIG

let gain = abs(v(out)[40])/abs(v(in2)[40])
let gaindb = 20*log10(abs(gain))

let ref=gaindb-3

meas AC low WHEN vdb(out) = ref
meas AC up WHEN vdb(out) = ref CROSS=LAST

let bandwidth=up-low

let cost_r=(RS_value+RB1_value+RB2_value+RC1_value+RE1_value+RE2_value+RL_value)/1000
let cost_c=(Ci_value+Cb_value+Co_value)*1000000
let cost_t=2
let cost=cost_r+cost_c+cost_t

let merit=((gain*bandwidth)/(cost*low))

echo "merit_TAB"
echo "Merit = $&merit"
echo "Cost = $&cost MU"
echo "merit_END"

echo "gain_TAB"
echo "Output Voltage Gain = $&gaindb"
echo "gain_END"

let Vce=(vdb(coll)[40]-vdb(emit)[40])
let Vbe=(vdb(base)[40]-vdb(emit)[40])
let Vec=vdb(emit2)[40]
let Veb=(vdb(emit2)[40]-vdb(coll)[40])
let Vcebe=Vce-Vbe
let Veceb=Vec-Veb

echo "FAR_TAB"
echo "VCE-VBE = $&Vcebe"
echo "VEC-VEB = $&Veceb"
echo "FAR_END"

let Vdb_col=vdb(coll)[40]
let Vdb_out=vdb(out)[40]

echo "resultscompar_TAB"
echo "VCE = $&Vce"
echo "VBE = $&Vbe"
echo "Vo1 = $&Vdb_col"
echo "Vo2 = $&Vdb_out"
echo "resultscompar_END"

echo "results_TAB"
echo "Low CO Freq = $&low"
echo "Up CO Freq = $&up"
echo "Bandwidth = $&bandwidth"
echo "results_END"

*input impedance in kohm
*plot input impedance
*hardcopy inputimpedance.ps abs(v(in2)[40]/vin#branch[40]/(-1000))

let Zi=abs(v(in2)[40]/vin#branch[40])

echo "inputZ_TAB"
echo "ZI = $&Zi"
echo "inputZ_END"

echo "valores_TAB"
echo "RS = $&RS_value"
echo "RB1 = $&RB1_value"
echo "RB2 = $&RB2_value"
echo "RC1 = $&RC1_value"
echo "RE1 = $&RE1_value"
echo "RL = $&RL_value"
echo "Ci = $&Ci_value"
echo "Cb = $&Cb_value"
echo "Co = $&Co_value"
echo "valores_END"

quit
.endc 

.end
